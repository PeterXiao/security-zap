plugins {
    id 'nu.studer.plugindev' version '1.0.3'
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
    maven {
        url 'http://dl.bintray.com/wei/maven'
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'commons-io:commons-io:2.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.1'
    compile 'org.apache.velocity:velocity:1.7'
    compile 'org.apache.velocity:velocity-tools:2.0'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

// for local debug use only
//uploadArchives {
//    repositories {
//        mavenDeployer {
//            repository(url: uri('../repo'))
//        }
//    }
//}

// upload to maven central
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Gradle zap plugin'
                packaging 'jar'
                description 'A gradle plugin that help you integrate ZAP Proxy into your CI server'
                url 'https://github.com/wmaintw/security-zap'

                scm {
                    connection 'https://github.com/wmaintw/security-zap.git'
                    developerConnection 'https://github.com/wmaintw/security-zap.git'
                    url 'https://github.com/wmaintw/security-zap'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'wmaintw'
                        name 'Wei Ma'
                        email 'wma@thoughtworks.com'
                    }
                }
            }
        }
    }
}

group = 'com.thoughtworks.tools'
version = '1.0.1'

plugindev {
    pluginId = 'security.zap'
    pluginName = 'security-zap'
    pluginImplementationClass 'com.thoughtworks.tools.security.zap.plugin.BuildWithZapPlugin'
    pluginDescription 'Gradle plugin that help you integrate ZAP Proxy into your CI server.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'zap', 'security', 'proxy'
    authorId 'wmaintw'
    authorName 'Wei Ma'
    authorEmail 'wma@thoughtworks.com'
    projectUrl 'https://github.com/wmaintw/security-zap'
    projectIssuesUrl 'https://github.com/wmaintw/security-zap/issues'
    projectVcsUrl 'https://github.com/wmaintw/security-zap.git'
    projectInceptionYear '2015'
    done()
}

bintray {
    user = bintrayUser
    key = bintrayUserKey
    pkg.repo = bintrayRepo
}